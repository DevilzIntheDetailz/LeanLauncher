/*
 *   Copyright (C) 2015. Kumaresan Rajeswaran
 *
 *   Licensed under the Apache License, Version 2.0 (the "License");
 *   you may not use this file except in compliance with the License.
 *   You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 *   Unless required by applicable law or agreed to in writing, software
 *   distributed under the License is distributed on an "AS IS" BASIS,
 *   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *   See the License for the specific language governing permissions and
 *   limitations under the License.
 *
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode System.getenv("BUILD_NUMBER") as Integer ?: 99999 // so you don't have to uninstall for dev builds
        versionName "1.0" + '.' + versionCode
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    signingConfigs {
        release {
            storeFile file("release.keystore")
            keyAlias "ReleaseKey"
            storePassword System.console().readLine("\nKeystore password: ")
            keyPassword System.console().readLine("\nKey password: ")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.cfg'
            signingConfig signingConfigs.release
            zipAlignEnabled true

            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def file = output.outputFile
                    output.outputFile = new File(file.parent, file.name.replace(".apk", "-v" + defaultConfig.versionName + ".apk"))
                }
            }
        }
    }
}

dependencies {
    compile 'com.android.support:support-v13:23.1.1'

		debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
		releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
		testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
}

